project(':repository') {
	apply from: 'databaseSetup.gradle'
	
	repositories {
		mavenCentral()
	}
	
    dependencies {
	    compile project(':model')
		runtime 'com.h2database:h2:1.3.170'
		testCompile 'junit:junit:4.11'
    }

	test {
		exclude '**/*IntegTest.class'
		testReportDir = file("$testReportDir/unit")
		testResultsDir = file("$testResultsDir/unit")
	}

	task integrationTest(type: Test) {
		description = 'Runs the integration tests.'
		group = 'verification'
		include '**/*IntegTest.class'
		testReportDir = file("$testReportDir/integration")
		testResultsDir = file("$testResultsDir/integration")
	} 
	
	tasks.withType(Test) {
        testLogging {
            showStandardStreams = true
			exceptionFormat 'full'
        }
    }

	startDatabase.dependsOn test
	integrationTest.dependsOn startAndPrepareDatabase
	stopDatabase.dependsOn integrationTest
	check.dependsOn stopDatabase
}